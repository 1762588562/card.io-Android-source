buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:1.0.0'
    }
}

apply plugin: 'com.android.application'

android {
    compileSdkVersion 21
    buildToolsVersion '21.1.2'

    signingConfigs {
        myConfig {
            storeFile file("example.keystore")
            storePassword "abc123"
            keyAlias "example-alias"
            keyPassword "abc123"
        }
    }

    buildTypes {
        debug {
            applicationIdSuffix ".debug"
        }

        release{
            minifyEnabled true
            proguardFile file('proguard.cfg')
            signingConfig signingConfigs.myConfig
        }
    }

    android.applicationVariants.all { variant ->
        variant.outputs.each { output ->
            def outputFile = output.outputFile
            if (outputFile != null && outputFile.name.endsWith('.apk')) {
                output.outputFile = new File(outputFile.parent, "card.io-sample-app-${variant.name}.apk")
            }
        }
    }

    sourceSets {
        main {
            manifest.srcFile 'AndroidManifest.xml'
            java.srcDirs = ['src']
            resources.srcDirs = ['src']
            res.srcDirs = ['res']
            assets.srcDirs = ['assets']
        }
    }
}

dependencies {
    compile project (':card.io')
}

